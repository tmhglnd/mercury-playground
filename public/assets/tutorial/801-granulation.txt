// === TUTORIAL 801: Granulation Basic ===
// Simple granular timestretching is achieved
// by quickly playing short grains of a sample
// changing the offset of the playback point 
// and detuning the grains
set tempo 120

// a position ramp up-down over 500 values
list pos sineFloat(500 1 0 0.5)

// small detuning for every sample playback
list detune randomFloat(500 0.92 1.08)

// try different sample names
new sample bongo name(grain)
    // a fast time interval and a short attack and release time for fade-in/out
    set grain time(1/32) shape(20 20)
    // set the start position from the list and random panning
    set grain start(pos) pan(random)
    // set the playback speed and gain
    set grain speed(detune) gain(1.2)
    // add some effects
    set grain fx(delay 2/32 3/32 0.9) fx(reverb 0.4 5)