// The first two measures of Beethovens 5th Symphony
// Using 3 synths with squarewave oscillators in 3 different octaves
// The notes are repeated in a list together with notelengths
// The timediv is used to divide quarter notes into 16th

// The key is C minor
set scale minor c
set tempo 70

// The notes are g, eb, g, d
list notes repeat([7 3 7 2] [3 1 3 1])
list lengths repeat([1/16 1/1] [3 1 3 1])
print notes lengths

// Three synths in different octaves playing the same notes
new synth square note(notes 1) 
new synth square note(notes)
new synth square note(notes -1)

// These settings are set for all the synths above
set all time(1/4) timediv([4 1 1 1]) play([1 1 1 1 0 0 0]) 
set all shape(1 lengths) super(0.1 3) fx(reverb 0.4 5) 